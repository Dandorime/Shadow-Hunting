# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.
define li = Character('Лина', color="#9933FF")
define ma = Character('Мама', color="#ff3366")
define bar = Character('Мистер Барни', color="#946c00")

# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.

image lina pajamas = "images/chars/Lina pajamas.png"
image lina casual = "images/chars/Lina casual.png"
image mom = "images/chars/Mom.png"

default table_clicked = False
default table_box_clicked = False
default mirror_clicked = False

screen Table(): 
    imagebutton:
        auto "images/imagebutton/lina_room/table_%s.png" 
        focus_mask True
        action [SetVariable("table_clicked", True), Hide("Table"), Call("table_reaction")]

screen TableBox(): 
    imagebutton:
        auto "images/imagebutton/lina_room/table_box_%s.png" 
        focus_mask True
        action [SetVariable("table_box_clicked", True), Hide("TableBox"), Call("table_box_reaction")]

screen Mirror(): 
    imagebutton:
        auto "images/imagebutton/lina_room/mirror_%s.png" 
        focus_mask True
        sensitive (table_clicked and table_box_clicked)  # Зеркало активно только после нажатия на стол и коробку
        action [SetVariable("mirror_clicked", True), Hide("Mirror"), Call("mirror_reaction")]

init: 
    $ left2 = Position(xalign=0.2)
    $ right2 = Position(xalign=0.7)

label start:
    call screen shadow_chapter("Глава 1", "Утро Лины")
    stop music

    play sound "audio/sound/alarm-clock.mp3"
    play music "audio/birds.mp3" loop volume 0.5


    scene bg room lina

    '''
    Когда будильник издал противный звук, означающий время пробуждения, я уже надевала теплую водолазку.

    В этом году осенью холодает быстрее обычного.

    Сегодня я проснулась аж на час раньше будильника {w}и решила, что это знак встать пораньше.{w} В конце концов, будет больше времени на сборы.

    Я быстро завязала привычный пучок на голове, чтобы волосы не мешали работе.{w} Иногда мне хочется попробовать новую прическу,{w} но в лаборатории нет места модницам
    '''

    play sound "audio/sound/footsteps.mp3"

    '''
    Я подошла к стулу и заглянула в лежащий на нем шоппер.{w} Стоит проверить, что я ничего не забыла.

    Обычно я таскаю с собой явно больше, чем нужно, но по мне так лучше перестраховаться.{w} Хотя Алекс и считает меня барахольщицей.
    '''

    li "Блин.{w} Куда-то делись защитные очки.{w} Вчера вечером я пыталась почистить их."
    
    call explore_room

    stop audio

    jump kitchen

    return

label explore_room:
    window hide
    show screen Table with dissolve
    show screen TableBox with dissolve
    show screen Mirror with dissolve
    
    # Бесконечный цикл, пока не выполнится условие
    while not (table_clicked and table_box_clicked and mirror_clicked):
        pause
    return

label table_reaction:
    window show
    li "Нет, здесь их точно нет.{w} Не люблю, когда на столе беспорядок.{w} Я бы точно не оставила их здесь."
    window hide
    return

label table_box_reaction:
    window show
    li "Странно{w}, здесь их нет.{w} Обычно все самое нужное я кладу сюда."
    window hide
    return

label mirror_reaction:
    window show
    li "..."

    "Защитные очки висели на большом зеркале у стены.{w} Я даже подумать не могла, что найду их здесь.{w} Я ведь почти не…"

    li "Наверное, мама заходила в комнату и убрала их с кровати."

    '''
    Я подошла к зеркалу и, протянув руку, забрала очки. Мой взгляд невольно прошелся по отражению в зеркале{w} – а как иначе?

    '''
    show lina pajamas at right2
    '''

    Как всегда{w}, я увидела знакомый силуэт напротив: худую девушку в черных джинсах и серой водолазке с пучком на голове.{w} Ничего примечательного{w}, как и всегда.

    Я подняла взгляд выше, на лицо, и тихо вздохнула.{w} Каждый раз мне кажется, что я стала видеть своё лицо чуть лучше, но потом я вспоминаю, как это глупо.

    Мне уже двадцать три, и я научилась жить со своим диагнозом и даже свыклась с мыслью, что так и не узнаю, как выглядит мое собственное лицо.{w} Но иногда мне все же интересно.

    Какой у меня нос?{w} Пухлые ли у меня губы?{w} И считаюсь ли я красивой?{w} Все вокруг говорят, что я достаточно симпатичная, я не привыкла верить на слово{w}, пока не смогу убедиться сама.

    Хотя в случае с моим диагнозом это звучит глупо.
    '''
    window hide
    return

label kitchen: 
    ma "Лина, ты уже встала?"

    '''
    Я слегка вздрогнула. Громкий голос мамы как всегда прервал мои размышления.

    Я быстро кинула очки в шоппер и пошла вниз на кухню.{w} Судя по бодрому голосу мамы, она выспалась, и у нее было хорошее настроение.
    '''

    play sound "audio/sound/ladder-steps.mp3"
    play music "audio/cook.mp3" loop

    scene bg kitchen
    with dissolve

    show mom at left2
    with dissolve
    show lina casual at right2
    with dissolve


    li "Привет"

    ma "Доброе утро, золотко!"

    "Мама энергично ходила по кухне, раскладывая кусочки сыра на тосты и наливая кофе из турки.{w} По мне так нет ничего лучше простого и вкусного завтрака."

    ma "Как спалось?{w} Ты сегодня рано, еще и половины девятого нет."

    "Я села за стол и уже собиралась ответить, но мама продолжала говорить."

    ma "Ну и холод на улице. Я хотела проветрить, но сразу замерзла."

    '''
    Она поставила на стол тарелки с тостами, а после и кружки с свежим кофе. Моя серая тарелка, из которой я всегда ела, уже немного поцарапалась.

    Я кивнула и сразу же потянулась к своему завтраку, пока мама садилась напротив и наливала в кофе сливки.

    '''

    li "Ну, уже середина октября.{w} Надо бы достать мою теплую шапку."

    '''
    Мама согласно кивнула, размешивая сахар в кофе.{w} Сегодня на ней была классическая рубашка.

    Кофе и тосты были особенно вкусными прохладным утром.{w} Я не торопилась с завтраком, зная, что скоро придется идти по холоду в институт.
    '''

    ma "Кстати, сегодня я читала медицинские новости с утра."

    "Ну вот, снова началось."

    ma "Одна из неврологических клиник в Портленде{w} объявила, что начинает несколько исследований в области нейрохирургии.{w} В том числе они и лицевую агнозию изучают."

    "Я немного сморщилась от названия собственного диагноза. Не люблю медицинские термины."

    li "И что с того?"

    ma "Ну как что!{w} Кто знает, может они найдут что-то полезное и применимое к диагнозу.{w} Или лекарства изобретут.{w} Лишним точно не будет"

    '''
    Я не разделяла оптимистичный настрой мамы, но и не хотела с ней спорить. 
    
    За эти годы я устала ездить по врачам, которые всегда говорили одно и то же. 
    
    В нашем маленьком городе нет профильных специалистов, а добираться в соседний Портленд по два часа мне ужасно надоедало.

    Я ведь не слепая{w}, просто{w} не различаю лица. 

    На слова мамы я пожала плечами, надеясь, что она не потащит меня к очередному неврологу.
    '''

    li "Может быть.{w} Наверное исследования займут много времени."

    ma "Ну и ладно, главное, что они начались.{w} Сейчас медицина современная{w}, точно что-то найдут."

    "Я улыбнулась и кивнула маме, а после продолжила есть свой завтрак. Часы показывали уже половину девятого утра, у меня еще было время немного побыть дома перед работой."

    menu:
        "Что сделать?"
        
        "Включить телевизор":
            jump watch_tv
            
        "Пойти в лабораторию пораньше":
            jump go_to_lab_early

    return

label watch_tv:

    hide mom 
    with dissolve

    play music 'audio/music/news.mp3'

    '''
    Я не была особым любителем смотреть телевизор, но перед выходом из дома делать было особо нечего. 
    
    Первым попавшимся был канал новостей нашего небольшого города, и я решила, что это отлично подойдет в качестве фона для завтрака.
    '''

    "Диктор" "…в этом месяце завод выпустил рекордное количество лекарств, которые направлены на…"

    '''
    Тема больниц, лекарств и врачей невольно преследовала меня всю жизнь. И не только из-за диагноза.

    Наш небольшой город Риджвью был известен большим фармацевтическим заводом. Химическая промышленность была хорошо развита, поэтому многие лекарства производились именно у нас.

    Неудивительно, что многие шли работать на завод или в местные лаборатории. Надеюсь, однажды и я найду для себя что-то более серьезное, чем работа в своем же универе.
    '''

    stop music

    '''
    Оттягивать начало рабочего дня было бессмысленно. Через пару минут я закончила завтрак, обняла маму и пошла в коридор.

    В доме все, как и всегда, было на своих местах. По памяти и даже не смотря я достала теплую шапку, а после намотала на шею шарф. Мое пальто всегда висело на левом крючке.
    '''

    ma "Хорошего дня!"
    play sound 'audio/sound/open_door.mp3'

    scene bg street
    with dissolve

    play music "audio/wind.mp3" loop

    '''
    Я вышла на улицу пошла в сторону университета. На самом деле мне нравилось учиться. Этим летом я защитила диплом, а с осени начала работать в лаборатории при вузе.

    Работа с реактивами была для меня идеальной. Конечно, в будущем мне бы хотелось чего-то большего, но сейчас я довольна тем, что имею.

    К тому же, я работаю вместе с друзьями, поэтому мне совсем не скучно. Смешивать реактивы не так нужно, когда рядом 

    Воздух на улице и правда был холодным. Как же хотелось, чтобы лето продлилось дольше.
    '''

    jump uniwer

    return

label go_to_lab_early:
    hide mom 
    with dissolve

    '''
    Оттягивать начало рабочего дня было бессмысленно. Через пару минут я закончила завтрак, обняла маму и пошла в коридор.

    В доме все, как и всегда, было на своих местах. По памяти и даже не смотря я достала теплую шапку, а после намотала на шею шарф. Мое пальто всегда висело на левом крючке.

    '''

    ma "Хорошего дня!"

    play sound 'audio/sound/open_door.mp3'

    scene bg street
    with dissolve

    play music "audio/wind.mp3" loop

    '''
    Я вышла на улицу пошла в сторону университета. На самом деле мне нравилось учиться. Этим летом я защитила диплом, а с осени начала работать в лаборатории при вузе.

    Работа с реактивами была для меня идеальной. Конечно, в будущем мне бы хотелось чего-то большего, но сейчас я довольна тем, что имею.

    К тому же, я работаю вместе с друзьями, поэтому мне совсем не скучно. Смешивать реактивы не так нужно, когда рядом 

    Воздух на улице и правда был холодным. Как же хотелось, чтобы лето продлилось дольше.
    '''
    
    jump uniwer

    return

label uniwer: 
    scene bg university
    play sound "audio/sound/step-street.mp3"

    '''
    Я ускорила шаг, зарываясь носом в шарф.{w} Морозный воздух приятно бодрил, но стоило немного зазеваться — и пальцы начинали неметь.
    
    По пути я вспомнила, что сегодня у нас работа с новыми образцами, и мне захотелось прийти в лабораторию пораньше, чтобы подготовить рабочее место.{w} Все-таки не зря я проснулась до будильника.

    Здание университета казалось еще полупустым: я не удивлена, ведь мало кто из студентов спешил приходить к первой паре. 
    '''

    stop sound
    scene bg entrance

    '''
    
    Вокруг была тишина и покой, я любила этот момент перед работой,{w} а когда было время - останавливалась перед входом здания и набиралась сил перед рабочим днем.

    Я поднялась по ступенькам и вошла в здание университета.{w} Как только я начала снимать шарф, меня окликнул знакомый голос.
    '''

    "???" "Мисс Донованн!"

    '''
    Я повернула голову, ко мне навстречу шел знакомый силуэт.
    
    Низкий рост,{w} немного лишнего веса,{w} неизменная коричневая жилетка и туфли,{w} которые почему-то всегда стучали по полу – ко мне шел мистер Барни, один из профессоров.

    Карлос Барни преподавал у меня на последних курсах и был известным химиком в нашем городе.{w} Сейчас мы часто виделись с ним, так как недавно его назначили ответственным за лабораторию нашего университета.

    Он подошел ко мне немного запыхавшимся, судя по отдышке.
    '''

    bar "Лина, доброе утро.{w} Хотел напомнить, что сегодня в лабораторию привезут новые вещества.{w} Я распорядился."

    return
